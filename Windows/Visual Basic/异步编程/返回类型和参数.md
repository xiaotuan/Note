### Task(T) 返回类型

[Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1) 返回类型用于某种异步方法，此异步方法包含 [Return](https://learn.microsoft.com/zh-cn/dotnet/visual-basic/language-reference/statements/return-statement) 语句，其中操作数具有类型 `TResult`。

在下面的示例中，`TaskOfT_MethodAsync` 异步方法包含返回整数的 return 语句。 因此，该方法声明必须指定 `Task(Of Integer)` 的返回类型。

```vb
' TASK(OF T) EXAMPLE
Async Function TaskOfT_MethodAsync() As Task(Of Integer)

    ' The body of an async method is expected to contain an awaited
    ' asynchronous call.
    ' Task.FromResult is a placeholder for actual work that returns a string.
    Dim today As String = Await Task.FromResult(Of String)(DateTime.Now.DayOfWeek.ToString())

    ' The method then can process the result in some way.
    Dim leisureHours As Integer
    If today.First() = "S" Then
        leisureHours = 16
    Else
        leisureHours = 5
    End If

    ' Because the return statement specifies an operand of type Integer, the
    ' method must have a return type of Task(Of Integer).
    Return leisureHours
End Function
```

该任务指派给示例中的 `integerTask` 变量。 因为 `integerTask` 是 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1)，所以它包含类型 `TResult` 的 [Result](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1.result#system-threading-tasks-task-1-result) 属性。 在这种情况下，TResult 表示整数类型。

> 警告：
>
> [Result](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1.result) 属性为阻止属性。 如果你在其任务完成之前尝试访问它，当前处于活动状态的线程将被阻止，直到任务完成且值为可用。 在大多数情况下，应通过使用 `Await` 访问此值，而不是直接访问属性。
>
> ```vb
> ' Call and await in separate statements.
> Dim integerTask As Task(Of Integer) = TaskOfT_MethodAsync()
> 
> ' You can do other work that does not rely on resultTask before awaiting.
> textBox1.Text &= "Application can continue working while the Task(Of T) runs. . . . " & vbCrLf
> 
> Dim result2 As Integer = Await integerTask
> ```
>
> 以下代码中的显示语句验证 `result1` 变量、`result2` 变量与 `Result` 属性的值是否相同。 请记住，`Result` 属性是锁定属性，在其任务完成之前不应访问。
>
> ```vb
> ' Display the values of the result1 variable, the result2 variable, and
> ' the resultTask.Result property.
> textBox1.Text &= vbCrLf & $"Value of result1 variable:   {result1}" & vbCrLf
> textBox1.Text &= $"Value of result2 variable:   {result2}" & vbCrLf
> textBox1.Text &= $"Value of resultTask.Result:  {integerTask.Result}" & vbCrLf
> ```

### Task 返回类型

不包含 return 语句或包含不返回操作数的 return 语句的异步方法通常具有返回类型 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task)。 如果被编写为异步运行，这些方法将为 [Sub](https://learn.microsoft.com/zh-cn/dotnet/visual-basic/programming-guide/language-features/procedures/sub-procedures) 过程。 如果在异步方法中使用 `Task` 返回类型，调用方法可以使用 `Await` 运算符暂停调用方的完成，直至被调用的异步方法结束。

在下面的示例中，异步方法 `Task_MethodAsync` 不包含 return 语句。 因此，为此方法指定 `Task` 返回类型，这将启用 `Task_MethodAsync` 为等待。 `Task` 类型的定义不包括存储返回值的 `Result` 属性。

```vb
' TASK EXAMPLE
Async Function Task_MethodAsync() As Task

    ' The body of an async method is expected to contain an awaited
    ' asynchronous call.
    ' Task.Delay is a placeholder for actual work.
    Await Task.Delay(2000)
    textBox1.Text &= vbCrLf & "Sorry for the delay. . . ." & vbCrLf

    ' This method has no return statement, so its return type is Task.
End Function
```

通过使用 await 语句而不是 await 表达式来调用和等待 `Task_MethodAsync`，类似于异步 `Sub` 或返回返回 void 的方法的调用语句。 `Await` 运算符的应用程序在这种情况下不生成值。

以下代码调用和等待方法 `Task_MethodAsync`。

```vb
' Call and await the Task-returning async method in the same statement.
Await Task_MethodAsync()
```

如同上一个 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1) 示例，可以从 `Await` 运算符的应用程序中分离对 `Task_MethodAsync` 的调用，如以下代码所示。 但是，请记住，`Task` 没有 `Result` 属性，并且当 await 运算符应用于 `Task` 时不产生值。

以下代码从等待 `Task_MethodAsync` 返回的任务中分离调用 `Task_MethodAsync`。

```vb
' Call and await in separate statements.
Dim simpleTask As Task = Task_MethodAsync()

' You can do other work that does not rely on simpleTask before awaiting.
textBox1.Text &= vbCrLf & "Application can continue working while the Task runs. . . ." & vbCrLf

Await simpleTask
```

### 返回类型为 void

`Sub` 过程主要用于没有返回类型（在其他语言中称为 void 返回类型）的事件处理程序中。 Void 返回还可用于替代返回 void 的方法，或者用于执行可分类为"发后不理"活动的方法。但是，你应尽可能地返回 `Task`，因为不能等待返回 void 的异步方法。 这种方法的任何调用方必须能够继续完成，而无需等待调用的异步方法完成，并且调用方必须独立于异步方法生成的任何值或异常。

返回 void 的异步方法的调用方无法捕获从该方法引发的异常，且此类未经处理的异常可能会导致应用程序故障。 如果返回 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task) 或 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1) 的异步方法中出现异常，此异常将存储于返回的任务中，并在等待该任务时再次引发。 因此，请确保可以产生异常的任何异步方法都具有返回类型 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task) 或 [Task](https://learn.microsoft.com/zh-cn/dotnet/api/system.threading.tasks.task-1)，并确保会等待对方法的调用。

若要详细了解如何在异步方法中捕获异常，请参阅 [Try...Catch...Finally 语句](https://learn.microsoft.com/zh-cn/dotnet/visual-basic/language-reference/statements/try-catch-finally-statement)。

下面的代码定义了异步事件处理程序。

```vb
' SUB EXAMPLE
Async Sub button1_Click(sender As Object, e As RoutedEventArgs) Handles button1.Click

    textBox1.Clear()

    ' Start the process and await its completion. DriverAsync is a
    ' Task-returning async method.
    Await DriverAsync()

    ' Say goodbye.
    textBox1.Text &= vbCrLf & "All done, exiting button-click event handler."
End Sub
```

